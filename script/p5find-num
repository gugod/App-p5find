#!/usr/bin/env perl
=head1 Abstract

p5find-num is a program for finding numerical literals

=head1 Description

Numerical literlas are tokens that looks like:

    1234       # decimal integer
    0b1110011  # binary integer
    01234      # octal integer
    0x1234     # hexadecimal integer

=cut


use v5.20;

use feature qw(signatures);
no warnings qw(experimental::signatures);

use App::p5find qw<iter_each p5_doc_iterator p5_find_iterator print_file_linenum_line>;

use Getopt::Long;

sub print_usage {
    print <<USAGE;
Usage: p5find-num [switches] [--] [dir...]
  -h    show this help message.

USAGE
}

my %opts;
GetOptions(
    \%opts,
    "h",
);

if ($opts{h}) {
    print_usage();
    exit(0);
}

my @paths = @ARGV;
@paths = ('.') unless @paths;

iter_each(
    p5_doc_iterator(@paths),
    sub ($doc) {
        my %matched;
        iter_each(
            p5_find_iterator($doc, sub { $_[1]->isa("PPI::Token::Number") }),
            sub ($num) {
                $matched{ $num->line_number } = 1;
            }
        );

        if (%matched) {
            print_file_linenum_line($doc->filename, \%matched);
        }
    }
);
