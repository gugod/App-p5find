#!/usr/bin/env perl
use v5.18;

use App::p5find qw<p5_doc_iterator print_file_linenum_line>;

use Getopt::Long 'GetOptions';

sub print_usage {
    print <<USAGE;
p5find-sub [switches] [path1] [path2]...
  -h        show help message
  --name    specify the name of subroutine.

For more documentation, see: perldoc p5find-sub
USAGE
}

my %opts;
GetOptions(
    \%opts,
    "h",
    "name=s",
);

if ($opts{h}) {
    print_usage();
    exit(0);
}

my ($wanted_sub_name, @paths);

if ($opts{name}) {
    $wanted_sub_name = $opts{name};
}

@paths = @ARGV;
@paths = ('.') unless @paths;

my $iter = p5_doc_iterator(@paths);
while (my $doc = $iter->()) {
    my $file = $doc->filename;

    my $o = $doc->find(
        sub {
            my $el = $_[1];
            return (
                $el->isa("PPI::Token::Word") && $el eq "sub" && (
                    $el->parent->isa("PPI::Statement::Sub") ||
                    $el->snext_sibling()->isa("PPI::Structure::Block")
                )
            );
        }
    ) or next;

    my %hits;
    for my $token (@$o) {
        # $token is the word "sub"
        my $el = $token->parent;
        my $sub_name = $el->isa("PPI::Statement::Sub") ? $el->name : "(anonymous)";

        if ( !defined($wanted_sub_name) || $wanted_sub_name eq $sub_name ) {
            my $ln = $token->line_number;
            $hits{$ln} = 1;
        }
    }

    if (%hits) {
        print_file_linenum_line( $file, \%hits );
    }
}
