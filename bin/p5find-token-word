#!/usr/bin/env perl
use v5.18;

use App::p5find qw(p5_doc_iterator);
use Getopt::Long;

my $word = shift(@ARGV);
my @args = @ARGV;
@args = ('.') unless @args;
for (@args) {
    my $iter = p5_doc_iterator($_);
    while ( defined ( my $doc = $iter->() ) ) {
        my %hits;
        my $tokens = $doc->find(
            sub {
                my $op = $_[1];
                return ($op->isa("PPI::Token::Word") && $op->content eq $word)
            }
        ) or next;

        for (my $i = 0; $i < @$tokens; $i++) {
            my $op = $tokens->[$i];
            my $ln = $op->line_number;
            $hits{$ln} = 1;
        }

        if (%hits) {
            my $file = $doc->filename;
            my $line_number = 0;
            open my $fh, "<", $file;
            while(my $line = <$fh>) {
                $line_number++;
                if ($hits{$line_number}) {
                    print "${file}:${line_number}:${line}";
                }
            }
            close($fh);
        }
    }
}
